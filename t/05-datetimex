use Test;
use Olson-TZ;
use DateTimeX;

plan 7;

my Olson-TZ $tz;

#Tue Apr 21 09:41:17 EDT 2015
#1429623677
#Tue Apr 21 09:41:17 EDT 2015
$tz .= new(name => 'America/New_York');
my DateTimeX $_21_Apr_2015_09_41_17 .=
    new(:year(2015), :month(4), :day(21),
        :hour(9), :minute(41), :second(17),
        :timezone($tz));
is($_21_Apr_2015_09_41_17.posix - 1429623677, 0, 'seconds since posix epoch');

# pick a timezone that isn't ours
my $off;
if $*TZ == -(4|5) * 60 * 60 {
    $tz .= new(name => 'America/Los_Angeles');
    $off = -8 * 60 * 60;
} else {
    $tz .= new(name => 'America/New_York');
    $off = -5 * 60 * 60;
}

my $time-change-utc   = DateTimeX.new(:year(2014), :month(3), :day(9), :hour(2), :minute(0), :second(0)).earlier(seconds => $off);
my $time-change-local = $time-change-utc.in-timezone($tz);
diag $time-change-utc;
diag $time-change-local;
is($time-change-local.hour, 3, "{$tz.name} standard -> summer transition in 2014");

$off += 60 * 60;
$time-change-utc   = DateTimeX.new(:year(2014), :month(11), :day(2), :hour(2), :minute(0), :second(0)).earlier(seconds => $off);
$time-change-local = $time-change-utc.in-timezone($tz);
diag $time-change-utc;
diag $time-change-local;
is($time-change-local.hour, 1, "{$tz.name} summer -> standard transition in 2014");

$tz .= new(name => 'Europe/Dublin');

diag 'Dublin summer time';
my DateTimeX  $utc-hot .= new(:year(2014), :month(6), :day(2), :hour(12));
my DateTimeX  $ist-hot .= new(:year(2014), :month(6), :day(2), :hour(13), :timezone($tz));	# summer time was in effect
diag 'utc: ' ~ $utc-hot.perl ~ "\n" ~ '.ie: ' ~$ist-hot.perl;
is $tz.utc-offset-in-seconds($utc-hot.posix), 60 * 60, 'IST summer offset';
is   $utc-hot.posix - $ist-hot.posix, 0, 'noon UTC is 1P IST in Dublin on summer days';

diag 'Dublin winter time';
my DateTimeX $utc-cold .= new(:year(2014), :month(2), :day(2), :hour(12));
my DateTimeX $gmt-cold .= new(:year(2014), :month(2), :day(2), :hour(12), :timezone($tz));	# standard/gm time was in effect
diag 'utc: ' ~ $utc-cold.perl ~ "\n" ~ '.ie: ' ~$gmt-cold.perl;
is $tz.utc-offset-in-seconds($utc-cold.posix), 0, 'IST winter offset';
is $utc-cold.posix - $gmt-cold.posix, 0, 'noon UTC is noon GMT in Dublin on winter days';
